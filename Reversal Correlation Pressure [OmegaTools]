Reversal Correlation Pressure [OmegaTools]

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © OmegaTools

//@version=6
indicator("Reversal Correlation Pressure [OmegaTools]")

lnt = input.int(21, 'Length')

corr = ta.correlation(high, low, lnt)
z = math.min(math.max((corr - ta.sma(corr, lnt * 5)) / ta.stdev(corr, lnt * 5), -0), 2)

zcol = z > 1 ? color.from_gradient(z, 1, 2, color.new(#1100fc, 30), color.new(#ee00ff, 0)) : color.new(color.gray, 50)

zscore = (close - ta.sma(close, lnt)) / ta.stdev(close, lnt)
long = zscore < -2
short = zscore > 2

backtester(long, short, cond1, cond2, x) =>
    var c1 = array.new_int()
    var c2 = array.new_int()
    if long[x]
        if cond1[x]
            if close > close[x]
                c1.push(1)
            else
                c1.push(0)
        if cond2[x]
            if close > close[x]
                c2.push(1)
            else
                c2.push(0)
    if short[x]
        if cond1[x]
            if close < close[x]
                c1.push(1)
            else
                c1.push(0)
        if cond2[x]
            if close < close[x]
                c2.push(1)
            else
                c2.push(0)
    if c1.size() > 250
        c1.remove(0)
    if c2.size() > 250
        c2.remove(0)
    wr1 = c1.sum() / c1.size() * 100
    wr2 = c2.sum() / c2.size() * 100
    output = 0
    if (wr1 > wr2 and cond1) or (wr2 > wr1 and cond2)
        if long
            output := 1
        if short
            output := -1
    output

signalmr = backtester(long, short, z > 1, z < 1, 10)
signaltf = backtester(short, long, z > 1, z <= 0, 10)
signal = signalmr + signaltf

plot(z != 0 ? z : na, 'Pressure', zcol, style = plot.style_columns)
plotarrow(signal, 'Filtered Signal', #2962ff, #e91e63, 0, 10, 15, force_overlay = true)
barcolor(signal > 0 ? #2962ff : signal < 0 ? #e91e63 : na)